plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'  // Required for google-services.json
}

android {
    namespace 'com.smartexpenseai.app'
    compileSdk 35

    defaultConfig {
        applicationId "com.smartexpenseai.app"
        minSdk 23
        targetSdk 35

        // Get version from Git
        def gitCommitCount = 101  // fallback
        def gitVersionName = "1.0.2"  // fallback

        try {
            def countResult = 'git rev-list --count HEAD'.execute()
            countResult.waitFor()
            if (countResult.exitValue() == 0) {
                gitCommitCount = Integer.parseInt(countResult.text.trim())
            }

            def descResult = 'git describe --tags --always --dirty'.execute()
            descResult.waitFor()
            if (descResult.exitValue() == 0) {
                def desc = descResult.text.trim()
                // Remove 'v' prefix and '-dirty' suffix
                gitVersionName = desc.replaceFirst('^v', '').replaceFirst('-dirty$', '')
                // If no tags, use commit count
                if (!desc.contains('-') && !desc.startsWith('v')) {
                    gitVersionName = "1.0.${gitCommitCount}"
                }
            }
        } catch (Exception e) {
            println "Warning: Git version detection failed, using fallback values"
        }

        versionCode gitCommitCount
        versionName gitVersionName

        testInstrumentationRunner "com.smartexpenseai.app.HiltTestRunner"

        println "Building version ${gitVersionName} (code: ${gitCommitCount})"
    }

    signingConfigs {
        release {
            // Production release keystore for Play Store - Smart Expense AI
            // SHA-1: 08:4C:85:1E:A0:0C:06:86:C5:10:9B:5C:6E:64:7C:68:AA:02:DA:50
            storeFile file("../smartexpense-release.keystore")
            storePassword 'SmartExpenseAI2024'
            keyAlias 'smartexpense-release'
            keyPassword 'SmartExpenseAI2024'
        }

        debug {
            // Debug builds continue using debug keystore
            // SHA-1: B9:E9:55:75:09:22:21:2E:5E:62:F6:23:AF:28:EB:41:86:8E:9F:A5
            storeFile file("../smartexpense-release.keystore")
            storePassword 'SmartExpenseAI2024'
            keyAlias 'smartexpense-release'
            keyPassword 'SmartExpenseAI2024'
        }
    }

    buildTypes {
        debug {
            // Mock mode enabled for development - bypasses authentication and subscription
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Production mode - uses real Google Sign-In and Billing
            buildConfigField "boolean", "MOCK_AUTH", "false"
            buildConfigField "boolean", "MOCK_SUBSCRIPTION", "false"
            buildConfigField "String", "BUILD_TYPE_NAME", '"Release"'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            // Production mode - uses real Google Sign-In and Billing
            buildConfigField "boolean", "MOCK_AUTH", "false"
            buildConfigField "boolean", "MOCK_SUBSCRIPTION", "false"
            buildConfigField "String", "BUILD_TYPE_NAME", '"Release"'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }
    
    
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "com.google.protobuf") {
            details.useVersion "3.24.4"
            details.because("Align protobuf with Android test runner expectations to avoid class verification issues")
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    
    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.4'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.4'
    
    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    
    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    
    // Room database dependencies
    implementation 'androidx.room:room-runtime:2.5.0'
    implementation 'androidx.room:room-ktx:2.5.0'
    implementation 'com.google.firebase:firebase-auth:22.3.1'  // Compatible with Kotlin 1.9
    implementation 'androidx.credentials:credentials:1.3.0'
    implementation 'androidx.credentials:credentials-play-services-auth:1.3.0'
    implementation 'com.google.android.libraries.identity.googleid:googleid:1.1.1'
    ksp 'androidx.room:room-compiler:2.5.0'
    
    // Charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    
    // Permissions
    implementation 'com.karumi:dexter:6.2.3'
    
    // SwipeRefreshLayout
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    
    // Network dependencies for AI Insights
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    
    // Shimmer loading effect
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    
    // Hilt Dependency Injection
    implementation 'com.google.dagger:hilt-android:2.48'
    ksp 'com.google.dagger:hilt-compiler:2.48'

    // Google Sign-In
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.3'

    // Keep Timber for now during migration (will be removed later)
    implementation 'com.jakewharton.timber:timber:5.0.1'
    
    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.12.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'com.google.dagger:hilt-android-testing:2.48'
    testImplementation 'androidx.room:room-testing:2.5.0'
    kspTest 'com.google.dagger:hilt-compiler:2.48'
    
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.48'
    kspAndroidTest 'com.google.dagger:hilt-compiler:2.48'
}

// Task to print current version
task printVersion {
    doLast {
        def vCode = 92
        def vName = "1.0.2"
        try {
            def countResult = 'git rev-list --count HEAD'.execute()
            countResult.waitFor()
            if (countResult.exitValue() == 0) {
                vCode = Integer.parseInt(countResult.text.trim())
            }
            def descResult = 'git describe --tags --always --dirty'.execute()
            descResult.waitFor()
            if (descResult.exitValue() == 0) {
                vName = descResult.text.trim().replaceFirst('^v', '').replaceFirst('-dirty$', '')
            }
        } catch (Exception e) {
            println "Warning: Could not get Git version"
        }

        println "============================================"
        println "App Version Information"
        println "============================================"
        println "Version Name: ${vName}"
        println "Version Code: ${vCode}"
        println "Git Commits:  ${vCode}"
        println "Git Describe: ${vName}"
        println "============================================"
    }
}
